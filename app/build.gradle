apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    defaultConfig {
        applicationId "io.zenandroid.greenfieldtemplate"
        minSdkVersion 22
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL", '"https://api.flickr.com/"'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "backend"

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            // At some point you might want to uncomment the next line
            // applicationIdSuffix = ".mock"
            dimension "backend"
        }
        prod {
            dimension "backend"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name == 'release' && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true);
        }
    }

}

//
// Workaround for testAnnotationProcessor bug, see https://gist.github.com/koral--/f99d64c72741c29431ce4e15c0e5b6d7
//
android.applicationVariants.all {
    def aptOutputDir = new File(buildDir, "generated/source/apt/${it.unitTestVariant.dirName}")
    it.unitTestVariant.addJavaSourceFoldersToModel(aptOutputDir)
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.android.support:appcompat-v7:25.4.0'
    implementation 'com.android.support:recyclerview-v7:25.4.0'
    implementation 'com.android.support:cardview-v7:25.4.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation 'com.jakewharton:butterknife:8.8.1'

    implementation "com.android.support.test.espresso:espresso-idling-resource:3.0.1"

    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation "io.reactivex.rxjava2:rxjava:2.1.6"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile "com.github.hotchemi:permissionsdispatcher:3.0.1"
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:3.0.1"

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    implementation 'com.google.dagger:dagger:2.12'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.12'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.10.0"
    testImplementation "org.hamcrest:hamcrest-all:1.3"

    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.12'

    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.1'
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.1') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
}
